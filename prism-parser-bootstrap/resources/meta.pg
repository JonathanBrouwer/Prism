rule toplevel -> Grammar = grammar(prule_action);

rule grammar(action) -> Grammar = GrammarFile(rs) <- rs:prule(action)*;

rule prule(action) -> PRule {
    Rule(name, a, params, type, blocks) <- a:"adapt"? "rule" name:identifier params:prule_params "->" type:prule_action_type "{" blocks:prule_body(action) "}";
    Rule(name, [], params, type, Block("", [], AnnotatedExpr([], expr) :: []) :: []) <- "rule" name:identifier params:prule_params "->" type:prule_action_type "=" expr:prule_expr(action) ";";
}

rule prule_params -> [Param] {
    params <- "(" params:#repeat(identifier, ",", *) ")";
    [] <- "";
}

rule prule_body(action) -> [Block] {
    prule_block(action)+;
    Block("", [], c) :: [] <- c:prule_constructors(action);
}

rule prule_block(action) -> Block {
    Block(n, a, c) <- a:"adapt"? "group" n:#str(identifier?) "{" c:prule_constructors(action) "}";
    Block(n, a, []) <- a:"adapt"? "group" n:#str(identifier?) ";";
}

rule prule_constructors(action) -> [AnnotatedPruleExpr] = prule_annotated_expr(action)*;
rule prule_annotated_expr(action) -> AnnotatedPruleExpr = AnnotatedExpr(ans, e) <- ans:<a <- a:prule_annotation>* e:prule_expr(action) ";";

rule prule_annotation -> Annotation {
    Error(err) <- "#" "[" "error" "(" err:pstring ")" "]";
    DisableLayout() <- "#" "[" "disable_layout" "]";
    EnableLayout() <- "#" "[" "enable_layout" "]";
    DisableRecovery() <- "#" "[" "disable_recovery" "]";
    EnableRecovery() <- "#" "[" "enable_recovery" "]";
}

rule prule_expr(action) -> PRuleExpr {
    group action {
        Action(r, a) <- a:action "<-" r:#this;
    }
    group choice {
        Choice(es) <- es:#repeat(#next, "/", 2, inf);
    }
    group sequence {
        Sequence(es) <- es:#repeat(#next, layout, 2, inf);
    }
    group bind {
        NameBind(n, e) <- n:identifier ":" e:#next;
    }
    group repeat {
        Repeat(r, "0", None(), Sequence([])) <- r:#this "*";
        Repeat(r, "1", None(), Sequence([])) <- r:#this "+";
        Repeat(r, "0", Some("1"), Sequence([])) <- r:#this "?";
    }
    group base {
        Literal(s) <- s:pstring;
        CharClass(c) <- "[" c:charclass "]";
        SliceInput(r) <- "#str" "(" r:prule_expr(action) ")";
        PosLookahead(r) <- "#pos" "(" r:prule_expr(action) ")";
        NegLookahead(r) <- "#neg" "(" r:prule_expr(action) ")";
        r <- "<" r:prule_expr(action) ">";
        This() <- "#this";
        Next() <- "#next";
        Guid() <- "#guid";
        AtAdapt(a, n) <- "#adapt" "(" a:identifier "," n:identifier ")";
        RunVar(n, as) <- n:identifier #neg(layout) "(" as:#repeat(prule_expr(action), ",", *) ")";
        RunVar(n, []) <- n:identifier;
        Repeat(e, "0", None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," "*" ")";
        Repeat(e, "1", None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," "+" ")";
        Repeat(e, min, None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," min:integer "," "inf" ")";
        Repeat(e, min, Some(max), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," min:integer "," max:integer ")";
    }
}

rule charclass -> CharClass {
    CharClass(negate, ps) <- negate:"^"? ps:#repeat(charclass_part, "|", *);
}

rule charclass_part -> Range {
    Range(c1, c2) <- "\'" c1:charclass_char "\'" "-" "\'" c2:charclass_char "\'";
    Range(c, c) <- "\'" c:charclass_char "\'";
}

rule charclass_char -> Input {
    [^ '\'' | '\\' | '\n'];
    char_escapes;
}

rule pstring -> Input {
    #[error("String")]
    #[disable_layout]
    #[disable_recovery]
    s <- "\"" s:#str(str_char*) "\"";
}

rule str_char -> Input {
    [^ '\"' | '\\' | '\n'];
    char_escapes;
}

rule char_escapes -> Input {
    "\n" <- "\\n";
    "\r" <- "\\r";
    "\\" <- "\\\\";
    "\"" <- "\\\"";
    "\'" <- "\\\'";
}

rule layout -> Unit {
    [' ' | '\r' | '\n'];
    "//" [^'\n']* "\n";
}

rule integer -> Input {
    #[error("Integer")]
    #[disable_layout]
    #[disable_recovery]
    #str([ '0'-'9' ]+);
}

rule identifier -> Input {
    #[error("Identifier")]
    #[disable_layout]
    #[disable_recovery]
    n <- #neg(reserved #neg(['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ])) n:#str([ 'a'-'z' | 'A'-'Z' | '_' ] ['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ]*);
}

rule reserved -> Unit {
    "end";
    "str";
    "rule";
    "ast";
    "neg";
    "pos";
    "group";
    "adapt";
}

rule prule_action -> PRuleAction {
    group cons {
        Construct("Cons", h :: t :: []) <- h:#next "::" t:#this;
    }
    group base {
        Construct("Nil", []) <- "[]";
        Construct(n, as) <- n:identifier "(" as:#repeat(prule_action, ",", *) ")";
        InputLiteral(s) <- s:pstring;
        Name(n) <- n:identifier;
        a <- "(" a:prule_action ")";
    }
}

rule prule_action_type -> PRuleActionType {
    List(t) <- "[" t:prule_action_type "]";
    Rule(t) <- "rule" t:prule_action_type;
    Input() <- "Input";
    Unit() <- "Unit";
    Name(n) <- n:identifier;
}