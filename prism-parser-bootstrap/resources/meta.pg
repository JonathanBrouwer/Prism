rule toplevel = grammar(prule_action);

rule grammar(action) = GrammarFile::GrammarFile(rs) <- rs:prule(action)*;

rule prule(action) {
    Rule::Rule(name, a, params, blocks) <- a:"adapt"? "rule" name:identifier params:prule_params "{" blocks:prule_body(action) "}";
    Rule::Rule(name, [], params, RuleBlock::Block("", [], AnnotatedRuleExpr::AnnotatedExpr([], expr) .. []) .. []) <- "rule" name:identifier params:prule_params "=" expr:prule_expr(action) ";";
}

rule prule_params {
    params <- "(" params:#repeat(identifier, ",", *) ")";
    [] <- "";
}

rule prule_body(action) {
    prule_block(action)+;
    RuleBlock::Block("", [], c) .. [] <- c:prule_constructors(action);
}

rule prule_block(action) {
    RuleBlock::Block(n, a, c) <- a:"adapt"? "group" n:#str(identifier?) "{" c:prule_constructors(action) "}";
    RuleBlock::Block(n, a, []) <- a:"adapt"? "group" n:#str(identifier?) ";";
}

rule prule_constructors(action) = prule_annotated_expr(action)*;
rule prule_annotated_expr(action) = AnnotatedRuleExpr::AnnotatedExpr(ans, e) <- ans:<a <- a:prule_annotation>* e:prule_expr(action) ";";

rule prule_annotation {
    RuleAnnotation::Error(err) <- "#" "[" "error" "(" err:pstring ")" "]";
    RuleAnnotation::DisableLayout() <- "#" "[" "disable_layout" "]";
    RuleAnnotation::EnableLayout() <- "#" "[" "enable_layout" "]";
    RuleAnnotation::DisableRecovery() <- "#" "[" "disable_recovery" "]";
    RuleAnnotation::EnableRecovery() <- "#" "[" "enable_recovery" "]";
}

rule prule_expr(action) {
    group action {
        RuleExpr::Action(r, a) <- a:action "<-" r:#this;
    }
    group choice {
        RuleExpr::Choice(es) <- es:#repeat(#next, "/", 2, inf);
    }
    group sequence {
        RuleExpr::Sequence(es) <- es:#repeat(#next, layout, 2, inf);
    }
    group bind {
        RuleExpr::NameBind(n, e) <- n:identifier ":" e:#next;
    }
    group repeat {
        RuleExpr::Repeat(r, "0", None(), RuleExpr::Sequence([])) <- r:#this "*";
        RuleExpr::Repeat(r, "1", None(), RuleExpr::Sequence([])) <- r:#this "+";
        RuleExpr::Repeat(r, "0", Some("1"), RuleExpr::Sequence([])) <- r:#this "?";
    }
    group base {
        RuleExpr::Literal(s) <- s:pstring;
        RuleExpr::CharClass(c) <- "[" c:charclass "]";
        RuleExpr::SliceInput(r) <- "#str" "(" r:prule_expr(action) ")";
        RuleExpr::PosLookahead(r) <- "#pos" "(" r:prule_expr(action) ")";
        RuleExpr::NegLookahead(r) <- "#neg" "(" r:prule_expr(action) ")";
        r <- "<" r:prule_expr(action) ">";
        RuleExpr::This() <- "#this";
        RuleExpr::Next() <- "#next";
        RuleExpr::Guid() <- "#guid";
        RuleExpr::AtAdapt(a, n) <- "#adapt" "(" a:identifier "," n:identifier ")";
        RuleExpr::RunVar(n, as) <- n:identifier #neg(layout) "(" as:#repeat(prule_expr(action), ",", *) ")";
        RuleExpr::RunVar(n, []) <- n:identifier;
        RuleExpr::Repeat(e, "0", None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," "*" ")";
        RuleExpr::Repeat(e, "1", None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," "+" ")";
        RuleExpr::Repeat(e, min, None(), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," min:integer "," "inf" ")";
        RuleExpr::Repeat(e, min, Some(max), d) <- "#repeat" "(" e:prule_expr(action) "," d:prule_expr(action) "," min:integer "," max:integer ")";
    }
}

rule charclass {
    CharClass::CharClass(negate, ps) <- negate:"^"? ps:#repeat(charclass_part, "|", *);
}

rule charclass_part {
    CharClassRange::Range(c1, c2) <- "\'" c1:charclass_char "\'" "-" "\'" c2:charclass_char "\'";
    CharClassRange::Range(c, c) <- "\'" c:charclass_char "\'";
}

rule charclass_char {
    [^ '\'' | '\\' | '\n'];
    char_escapes;
}

rule pstring {
    #[error("String")]
    #[disable_layout]
    #[disable_recovery]
    s <- "\"" s:#str(str_char*) "\"";
}

rule str_char {
    [^ '\"' | '\\' | '\n'];
    char_escapes;
}

rule char_escapes {
    "\n" <- "\\n";
    "\r" <- "\\r";
    "\\" <- "\\\\";
    "\"" <- "\\\"";
    "\'" <- "\\\'";
}

rule layout {
    [' ' | '\r' | '\n'];
    "//" [^'\n']* "\n";
}

rule integer {
    #[error("Integer")]
    #[disable_layout]
    #[disable_recovery]
    #str([ '0'-'9' ]+);
}

rule identifier {
    #[error("Identifier")]
    #[disable_layout]
    #[disable_recovery]
    n <- #neg(reserved #neg(['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ])) n:#str([ 'a'-'z' | 'A'-'Z' | '_' ] ['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ]*);
}

rule reserved {
    "end";
    "str";
    "rule";
    "ast";
    "neg";
    "pos";
    "group";
    "adapt";
}

rule prule_action {
    group cons {
        RuleAction::Construct("ParsedList", "Cons", h .. t .. []) <- h:#next ".." t:#this;
    }
    group base {
        RuleAction::Construct("ParsedList", "Nil", []) <- "[]";
        RuleAction::Construct(ns, n, as) <- ns:identifier "::" n:identifier "(" as:#repeat(prule_action, ",", *) ")";
        RuleAction::Construct("ActionResult", n, as) <- n:identifier "(" as:#repeat(prule_action, ",", *) ")";
        RuleAction::InputLiteral(s) <- s:pstring;
        RuleAction::Name(n) <- n:identifier;
        a <- "(" a:prule_action ")";
    }
}