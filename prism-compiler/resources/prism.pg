rule wrapped_expr = RuleAction::Value("Expr", e) <- e:expr;

rule expr {
    group statement {
        Expr::Let("___#GRAMMAR#___", e, b) <- "adapt" e:#next ";" el:$Expr::Name("___#GRAMMAR#___") b:#adapt(Expr, el, expr);
        Expr::Let(n, v, b)
            <- "let" n:identifier "=" v:#next ";" b:#this;
        Expr::Let(n, Expr::TypeAssert(v, t), b)
            <- "let" n:identifier ":" t:#next "=" v:#next ";" b:#this;
    }
    group fnconstruct {
        fnconstruct_chain(#this, #next);
    }
    group fntype {
        fntype_chain(#this, #next);
    }
    group assert {
        Expr::TypeAssert(e, typ)
            <- e:#next ":" typ:#next;
    }
    group fndestruct {
        Expr::FnDestruct(f, a)
            <- f:#this " " a:#next;
    }
    group base {
        t <- "(" t:expr ")";
        Expr::Type() <- "Type";
        Expr::GrammarType() <- "Grammar";
        Expr::GrammarValue(g) <- "grammar" "{" g:grammar(wrapped_expr) "}";
        Expr::Include(n) <- "include" "!" "(" n:identifier ")";
        Expr::Name(n) <- n:identifier;
    }
}

rule fnconstruct_chain(this, next) {
    Expr::FnConstruct(n, Expr::Let("_", Expr::TypeAssert(Expr::Name(n), t), r))
        <- "(" n:identifier ":" t:next ")" r:#this;
    Expr::FnConstruct(n, r)
        <- n:identifier r:#this;
    Expr::FnConstruct(n, Expr::Let("_", Expr::TypeAssert(Expr::Name(n), t), r))
        <- "(" n:identifier ":" t:next ")" "=>" r:this;
    Expr::FnConstruct(n, r)
        <- n:identifier "=>" r:this;
}

rule fntype_chain(this, next) {
    Expr::FnType(n, t, r)
        <- "(" n:identifier ":" t:this ")" r:#this;
    Expr::FnType(n, t, r)
        <- "(" n:identifier ":" t:this ")" "->" r:this;
    Expr::FnType("_", t, r)
        <- t:this r:#this;
    Expr::FnType("_", t, r)
        <- t:this "->" r:this;
}

rule layout {
    [' ' | '\n'];
    "//" [^'\n']* "\n";
}

rule keyword {
    "Type";
    "Grammar";
    "grammar";
}

rule identifier {
    #[error("Identifier")]
    #[disable_layout]
    #[disable_recovery]
    n <- #neg(keyword #neg(['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ]))
         n:#str([ 'a'-'z' | 'A'-'Z' | '_' ] ['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ]*);
}