rule toplevel:
    GrammarFile(rs) <- _n* rs:(prule*)

rule prule:
    Rule(name, blocks) <- "rule" name:identifier ":" _n blocks:prule_blocks
    Rule(name, Block("", AnnotatedExpr([], expr) :: []) :: []) <- "rule" name:identifier "=" expr:prule_expr _n

rule prule_blocks:
    (Block(n, c) <- "--" (n:identifier / n:"") _n c:prule_constructors)+
    Block("", c) :: [] <- c:prule_constructors

rule prule_constructors = prule_annotated_expr*
rule prule_annotated_expr = AnnotatedExpr(ans, e) <- ans:(a <- a:prule_annotation _n)* e:prule_expr _n

rule prule_annotation:
    Error(err) <- "@error" "(" err:pstring ")"
    DisableLayout() <- "@disable_layout"
    EnableLayout() <- "@enable_layout"
    DisableRecovery() <- "@disable_recovery"
    EnableRecovery() <- "@enable_recovery"

rule prule_expr:
    --
    Action(r, a) <- a:prule_action "<-" r:@this
    --
    Choice(es) <- es:@repeat(@next, "/", 2, inf)
    --
    Sequence(es) <- es:@repeat(@next, "", 2, inf)
    --
    NameBind(n, e) <- n:identifier ":" e:@next
    --
    Repeat(r, "0", None(), Sequence([])) <- r:@this "*"
    Repeat(r, "1", None(), Sequence([])) <- r:@this "+"
    Repeat(r, "0", Some("1"), Sequence([])) <- r:@this "?"
    --
    Literal(s) <- s:pstring
    CharClass(c) <- "[" c:charclass "]"
    SliceInput(r) <- "@str" "(" r:prule_expr ")"
    PosLookahead(r) <- "@pos" "(" r:prule_expr ")"
    NegLookahead(r) <- "@neg" "(" r:prule_expr ")"
    r <- "(" r:prule_expr ")"
    AtThis() <- "@this"
    AtNext() <- "@next"
    AtGrammar() <- "@grammar"
    AtAdapt(a, n) <- "@adapt" "(" a:prule_action "," n:identifier ")"
    Rule(n) <- n:identifier
    Repeat(e, "0", None(), d) <- "@repeat" "(" e:prule_expr "," d:prule_expr "," "*" ")"
    Repeat(e, "1", None(), d) <- "@repeat" "(" e:prule_expr "," d:prule_expr "," "+" ")"
    Repeat(e, min, None(), d) <- "@repeat" "(" e:prule_expr "," d:prule_expr "," min:integer "," "inf" ")"
    Repeat(e, min, Some(max), d) <- "@repeat" "(" e:prule_expr "," d:prule_expr "," min:integer "," max:integer ")"

rule prule_action:
    --
    Cons(h, t) <- h:@next "::" t:@this
    --
    Nil() <- "[]"
    Construct(n, []) <- n:identifier "(" ")"
    Construct(n, as) <- n:identifier "(" as:@repeat(prule_action, ",", *) ")"
    InputLiteral(s) <- s:pstring
    Name(n) <- n:identifier
    a <- "(" a:prule_action ")"

rule charclass:
    CharClass(negate, ps) <- negate:"^"? ps:@repeat(charclass_part, "|", *)

rule charclass_part:
    Range(c1, c2) <- "\'" c1:charclass_char "\'" "-" "\'" c2:charclass_char "\'"
    Range(c, c) <- "\'" c:charclass_char "\'"

rule charclass_char:
    [^ '\'' | '\\' | '\n']
    char_escapes

rule pstring:
    @error("String")
    @disable_layout
    @disable_recovery
    s <- "\"" s:@str(str_char*) "\""

rule str_char:
    [^ '\"' | '\\' | '\n']
    char_escapes

rule char_escapes:
    "\n" <- "\\n"
    "\r" <- "\\r"
    "\\" <- "\\\\"
    "\"" <- "\\\""
    "\'" <- "\\\'"

rule layout = [' ']

rule _n = ['\n']+

rule integer:
    @error("Integer")
    @disable_layout
    @disable_recovery
    n <- n:@str([ '0'-'9' ]+)

rule identifier:
    @error("Identifier")
    @disable_layout
    @disable_recovery
    n <- @neg(reserved @neg(['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ])) n:@str([ 'a'-'z' | 'A'-'Z' | '_' ]['a'-'z' | 'A'-'Z' | '0'-'9' | '_' ]*)
rule reserved = "end" / "str" / "rule" / "ast" / "neg" / "pos"
